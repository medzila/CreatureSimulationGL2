Implementation = FM (I_CreatureSimulator : Engine I_Creature [EnergySource];
		  Engine : [SleepDuration] [Duration] Visual;
		  I_Creature : C_Headcount Behaviour Movement [Color] [LengthOfView] [FieldOfView];
		  EnergySource : E_Headcount Size;
		  Behaviour : (Emerging|Energy|Predator|Random|Stupid|EmergingThenEnergy|EmergingThenPredator);
		  Movement : (Torus|Bouncing);
		  Visual : (I_Snapshot|I_RealTime);
		  C_Headcount : (C_1|C_25|C_50);
		  E_Headcount : (E_1|E_5|E_10);
		  Color : (Cube|Unique);
		  LengthOfView : (LOV_50|LOV_75|LOV_100);
		  FieldOfView : (FOV_45|FOV_90|FOV_360);
		  Size : (S_20|S_50|S_1000);
		  SleepDuration : (SD_10|SD_0|SD_200|SD_1000);
		  Duration : (D_1000|D_5000|D_10000);
		  I_SnapShot->SD_0;
		  I_SnapShot->Duration;
		  EmergingThenEnergy-> EnergySource;
		  Energy->EnergySource;
		  S_1000->E_1;)
		  
Domaine = FM (D_CreatureSimulator : Affichage D_Creature [Demo];
		  Affichage : [D_RealTime] [D_Snapshot] [Debug];
		  D_Snapshot : (Court|Moyen|Long);
		  D_RealTime : (Lente|Rapide|TresRapide);
		  D_Creature : Type Nombre;
		  Type: (Troupeau|Rebond|Predateur|Recherche);
		  Nombre: (Seul|Peu|Beaucoup);
		  Demo : (DemoParametrable|DemoNonParametrable);
		  DemoParametrable : (ModelePredateur|ModeleRecherche);
		  DemoNonParametrable : (MortIneluctable|Immortels);
		  ModelePredateur -> !Seul;
		  ModelePredateur -> Predateur;
		  MortIneluctable -> Seul;
		  MortIneluctable -> Rebond;
		  MortIneluctable -> Court;
		  Immortels -> Peu;
		  Immortels -> Troupeau;
		  Immortels -> Long;
		  ModeleRecherche -> Recherche;)
		  

regles = constraints(Court <-> D_1000;
					Moyen <-> D_5000;
					Long <-> D_10000;
					TresRapide <-> SD_10;
					Rapide <-> SD_200;
					Lente <-> SD_1000;
					D_Snapshot -> I_Snapshot;
					!D_Snapshot -> I_RealTime;
					Seul <-> C_1;
					Peu <-> C_25;
					Beaucoup <-> C_50;
					Predateur -> Predator;
					Predateur -> Torus;
					Predateur -> FOV_90;
		  			Predateur -> LOV_100;
					Rebond -> Bouncing;
					Rebond -> Stupid;
					Troupeau -> Emerging;
					Troupeau -> Torus;
					Troupeau -> !C_1;
					Troupeau -> FOV_45;
		  			Troupeau -> LOV_75;
					Recherche -> Energy;
					Recherche -> Torus;
					Recherche -> FOV_45;
		  			Recherche -> LOV_50;
					MortIneluctable -> E_1;
					MortIneluctable -> S_1000;
					Immortels -> C_25;
					Immortels -> E_10;
					Immortels -> S_50;
					Immortels -> Torus;)
fmSimu = aggregate { Implementation Domaine } withMapping regles

